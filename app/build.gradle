apply plugin: 'com.android.application'

android {
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
    }

// Load signing properties from external file.
    def signingProperties = new Properties()
    signingProperties.load(new FileInputStream(rootProject.file("signing.properties")))

    signingConfigs {
        release {
            storeFile rootProject.file(signingProperties.KeyStore)
            storePassword signingProperties.KeyStorePassword
            keyAlias signingProperties.KeyAlias
            keyPassword signingProperties.KeyPassword
        }
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.hva.m2mobi.m2hva_reservationsystem"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        android.defaultConfig.vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        acc {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            renderscriptDebuggable false
            minifyEnabled true
            applicationIdSuffix 'acc'
            versionNameSuffix 'acc'
        }
        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.release
            renderscriptDebuggable false
            minifyEnabled true
            pseudoLocalesEnabled true
            applicationIdSuffix 'release'
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        buildtest {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.release
            renderscriptDebuggable true
            minifyEnabled true
            pseudoLocalesEnabled false
            applicationIdSuffix 'test'
            versionNameSuffix 'test'
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

ext {
    fireBaseVersion = '16.0.4'
    compileVersion = "28.0.0"
    testVersion = "1.0.2"
    googleApiVersion = "1.23.0"
    butterKnifeVersion = "9.0.0-rc1"
}

configurations.all {
    resolutionStrategy.force 'com.android.support:support-annotations:23.1.0'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Main dependencies
    implementation "com.android.support:appcompat-v7:${compileVersion}"
    implementation "com.android.support:design:${compileVersion}"
    implementation "com.android.support:recyclerview-v7:${compileVersion}"
    implementation "com.android.support:gridlayout-v7:${compileVersion}"
    implementation "com.android.support:support-annotations:${compileVersion}"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'pub.devrel:easypermissions:0.3.0'
    implementation "com.jakewharton:butterknife:${butterKnifeVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterKnifeVersion}"
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Firebase
    implementation "com.google.firebase:firebase-auth:${fireBaseVersion}"
    implementation "com.google.firebase:firebase-core:${fireBaseVersion}"
    implementation "com.google.firebase:firebase-database:${fireBaseVersion}"

    //Google
    implementation 'com.google.android.gms:play-services-auth:16.0.1'
    implementation("com.google.api-client:google-api-client-android:${googleApiVersion}") {
        exclude group: 'org.apache.httpcomponents'
    }
    implementation("com.google.apis:google-api-services-calendar:v3-rev305-${googleApiVersion}") {
        exclude group: 'org.apache.httpcomponents'
    }

    //Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "com.android.support.test:runner:${testVersion}"
    androidTestImplementation "com.android.support.test:rules:${testVersion}"
    androidTestImplementation "com.android.support:support-annotations:${compileVersion}"
    androidTestImplementation 'com.google.code.findbugs:jsr305:3.0.2'

    //API calls library
    implementation "io.reactivex.rxjava2:rxjava:2.2.3"
    implementation 'com.squareup.retrofit2:retrofit:2.5.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'


}

apply plugin: 'com.google.gms.google-services'

